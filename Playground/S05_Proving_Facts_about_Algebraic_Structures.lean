import Mathlib.Topology.MetricSpace.Basic
import Paperproof

section
variable {α : Type*} [PartialOrder α]
variable (x y z : α)

#check x ≤ y
#check (le_refl x : x ≤ x)
#check (le_trans : x ≤ y → y ≤ z → x ≤ z)
#check (le_antisymm : x ≤ y → y ≤ x → x = y)


#check x < y
#check (lt_irrefl x : ¬ (x < x))
#check (lt_trans : x < y → y < z → x < z)
#check (lt_of_le_of_lt : x ≤ y → y < z → x < z)
#check (lt_of_lt_of_le : x < y → y ≤ z → x < z)

example : x < y ↔ x ≤ y ∧ x ≠ y :=
  lt_iff_le_and_ne

end

section
variable {α : Type*} [Lattice α]
variable (x y z : α)

#check x ⊓ y
#check (inf_le_left : x ⊓ y ≤ x)
#check (inf_le_right : x ⊓ y ≤ y)
#check (le_inf : z ≤ x → z ≤ y → z ≤ x ⊓ y)
#check x ⊔ y
#check (le_sup_left : x ≤ x ⊔ y)
#check (le_sup_right : y ≤ x ⊔ y)
#check (sup_le : x ≤ z → y ≤ z → x ⊔ y ≤ z)

example : x ⊓ y = y ⊓ x := by
  apply le_antisymm
  repeat
    apply le_inf
    exact inf_le_right
    exact inf_le_left

example : x ⊓ y ⊓ z = x ⊓ (y ⊓ z) := by
  apply le_antisymm
  · apply le_inf
    · apply le_trans
      · apply inf_le_left
      apply inf_le_left
    · apply le_inf
      · apply le_trans
        · apply inf_le_left
        apply inf_le_right
      · apply inf_le_right
  · apply le_inf
    · apply le_inf
      · apply inf_le_left
      · apply le_trans
        · apply inf_le_right
        apply inf_le_left
    · apply le_trans
      · apply inf_le_right
      apply inf_le_right

example : x ⊔ y = y ⊔ x := by
  apply le_antisymm
  repeat
    apply sup_le
    apply le_sup_right
    apply le_sup_left


example : x ⊔ y ⊔ z = x ⊔ (y ⊔ z) := by
  apply le_antisymm
  · apply sup_le
    · apply sup_le
      · apply le_sup_left
      apply le_trans
      · change y ≤ y ⊔ z
        apply le_sup_left
      · apply le_sup_right
    · apply le_trans
      · change z ≤ y ⊔ z
        apply le_sup_right
      apply le_sup_right
  · apply sup_le
    · apply le_trans
      · change x ≤ x ⊔ y
        apply le_sup_left
      · apply le_sup_left
    · apply sup_le
      · apply le_trans
        · change y ≤ x ⊔ y
          apply le_sup_right
        · apply le_sup_left
      · apply le_sup_right


theorem absorb1 : x ⊓ (x ⊔ y) = x := by
  apply le_antisymm
  · apply inf_le_left
  · apply le_inf
    · rfl
    apply le_sup_left

theorem absorb2 : x ⊔ x ⊓ y = x := by
  apply le_antisymm
  · apply sup_le
    · rfl
    apply inf_le_left
  apply le_sup_left

end

section
variable {α : Type*} [DistribLattice α]
variable (x y z : α)

#check (inf_sup_left x y z : x ⊓ (y ⊔ z) = x ⊓ y ⊔ x ⊓ z)
#check (inf_sup_right x y z : (x ⊔ y) ⊓ z = x ⊓ z ⊔ y ⊓ z)
#check (sup_inf_left x y z : x ⊔ y ⊓ z = (x ⊔ y) ⊓ (x ⊔ z))
#check (sup_inf_right x y z : x ⊓ y ⊔ z = (x ⊔ z) ⊓ (y ⊔ z))
end

section
variable {α : Type*} [Lattice α]
variable (a b c : α)

example (h : ∀ x y z : α, x ⊓ (y ⊔ z) = x ⊓ y ⊔ x ⊓ z) : a ⊔ b ⊓ c = (a ⊔ b) ⊓ (a ⊔ c) := by
  rw [h, inf_comm _ a, inf_comm (a ⊔ b) c, inf_sup_self, h, ← sup_assoc, inf_comm c a]
  rw [sup_inf_self, inf_comm c b]

#check inf_sup_self

example (h : ∀ x y z : α, x ⊔ y ⊓ z = (x ⊔ y) ⊓ (x ⊔ z)) : a ⊓ (b ⊔ c) = a ⊓ b ⊔ a ⊓ c := by
  rw [h, sup_comm _ a, sup_inf_self, sup_comm (a ⊓ b), h, ← inf_assoc, sup_comm c a]
  rw [inf_sup_self, sup_comm c b]

end

section
variable {R : Type*} [Ring R] [PartialOrder R] [IsStrictOrderedRing R]
variable (a b c : R)

#check (add_le_add_left : a ≤ b → ∀ c, c + a ≤ c + b)
#check (mul_pos : 0 < a → 0 < b → 0 < a * b)

#check (mul_nonneg : 0 ≤ a → 0 ≤ b → 0 ≤ a * b)

example (h : a ≤ b) : 0 ≤ b - a := by
  have h₁ : a + -a ≤ b + -a := add_le_add_right h (-a)
  rw [← add_neg_cancel a, sub_eq_add_neg]
  assumption

example (h : 0 ≤ b - a) : a ≤ b := by
  rw [← zero_add a, ← sub_add_cancel b a]
  apply add_le_add_right
  assumption

example (h : a ≤ b) (h' : 0 ≤ c) : a * c ≤ b * c := by
  have h₀ : 0 ≤ b - a := by
    rw [← add_neg_cancel a, sub_eq_add_neg]
    apply add_le_add_right
    assumption
  have h₁ : 0 ≤ (b - a) * c := mul_nonneg h₀ h'
  calc
    a * c  ≤ (b - a) * c + a * c  := by exact (le_add_iff_nonneg_left (a * c)).mpr h₁
    _ = b * c - a * c + a * c := by
      rw [sub_mul]
    _ = b * c := sub_add_cancel (b * c) (a * c)

end

section
variable {X : Type*} [MetricSpace X]
variable (x y z : X)

#check (dist_self x : dist x x = 0)
#check (dist_comm x y : dist x y = dist y x)
#check (dist_triangle x y z : dist x z ≤ dist x y + dist y z)

example : (0 ≤ dist x y) := by
  have h : 0 ≤ 2 * dist x y := by
    calc
      0 = dist x x := Eq.symm (dist_self x)
      _ ≤ dist x y + dist y x := dist_triangle x y x
      _ = 2 * dist x y := by
        nth_rw 1 [add_comm, dist_comm]
        linarith
  linarith

end
